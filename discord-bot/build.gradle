import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'org.jetbrains.kotlin.jvm'
    id "org.jetbrains.kotlin.plugin.serialization" version "$kotlin_version"
}

group = 'com.github.ml404'
version = '6.0-SNAPSHOT'
mainClassName = "bot.Application"

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://repo.spring.io/libs-snapshot" }
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
    maven { name 'm2-dv8tion'; url 'https://m2.dv8tion.net/releases' }
    maven { url = uri("https://plugins.gradle.org/m2/") }
    maven { url = "https://maven.lavalink.dev/releases" }
    maven {
        name 'm2-duncte123'
        url 'https://m2.duncte123.dev/releases'
    }
}

configurations {
    configureEach {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
    }
}

dependencies {
    implementation project(":common")
    implementation project(":database")
    compileOnly 'org.jetbrains:annotations:24.0.0'
    implementation "net.dv8tion:JDA:5.0.2"
    implementation 'dev.arbjerg:lavaplayer:2.2.1'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'me.duncte123:botCommons:3.1.18'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-json:$ktor_version"
    implementation("io.ktor:ktor-serialization-kotlinx-json:$ktor_version")
    implementation "io.ktor:ktor-client-serialization:$ktor_version"
    implementation("io.ktor:ktor-client-content-negotiation:$ktor_version")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.3")
    testImplementation "io.ktor:ktor-client-mock:$ktor_version"
    implementation 'org.postgresql:postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.springframework:spring-jdbc'
    implementation 'org.springframework:spring-orm'
    implementation 'org.springframework:spring-context'
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.10"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_version"
    implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
    implementation 'org.springdoc:springdoc-openapi-kotlin:1.7.0'
    implementation 'dev.lavalink.youtube:common:1.7.2'
    runtimeOnly 'dev.arbjerg:lavaplayer-natives:2.2.1'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinx_version"
    testImplementation project(path: ':database', configuration: 'testArtifacts')
    testImplementation project(path: ':common', configuration: 'testArtifacts')
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.register('deleteExtras', Delete) {
    doLast {
        def libsDir = file("build/libs")
        def tmpDir = file("build/tmp")
        def distributions = file("build/distributions")
        tmpDir.eachFile { file -> file.delete() }
        distributions.eachFile { file -> file.delete() }
        libsDir.eachFile { file ->
            if (file.name.endsWith("SNAPSHOT.jar")) {
                println "Keeping ${file.name}"
            } else {
                file.delete()
                println "Deleted ${file.name}"
            }
        }
    }
}

tasks.named('build').configure {
    finalizedBy tasks.named('deleteExtras')
}

bootJar {
    launchScript()
    mainClassName = 'bot.Application'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "22"
    }
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(22)
}

configurations {
    testArtifacts
}

task testJar(type: Jar) {
    archiveClassifier = 'tests'  // Naming the JAR with a 'tests' classifier
    from sourceSets.test.output  // Include test classes in the JAR
}

artifacts {
    testArtifacts testJar
}
